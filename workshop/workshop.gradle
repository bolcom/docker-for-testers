import java.text.SimpleDateFormat

plugins {
    id "org.asciidoctor.convert" version "1.5.3"
    id "com.github.jruby-gradle.base" version "1.3.0"
    id "distribution"
}

dependencies {
    gems 'rubygems:asciidoctor-diagram:1.5.1'
}

def imageName = "registry.dev.bol.com/training-it/training-template"
def dockerBuildDir = "${buildDir}/docker"
def dockerContainerName = rootProject.name - ~/[^a-zA-Z0-9_]/
def asciidoctorOutputDir = "$buildDir/asciidoc"

def excludePatterns = [
        '**/target/**',
        '**/.asciidoctor/**',
        '**/diag-*.png*',
        '**/build/**',
        '**/.idea',
        '**/*.iml',
]

asciidoctorj { version = '1.5.4.1' }
asciidoctor {
    sourceDir = rootDir
    sources {
        ['labs', 'solutions', projectDir.name].each { dir ->
            include "**/${dir}/**/*.adoc"
            excludePatterns.each { pattern ->
                exclude "${dir}/${pattern}"
            }
        }
    }
    resources {
        from(projectDir) {
            include '/src/asciidoc/images/**'
            excludePatterns.each { pattern ->
                exclude "${pattern}"
            }
        }
        into project.name
    }
    outputDir = file(asciidoctorOutputDir)

    backends 'html5'
    options doctype: 'book'
    attributes 'source-highlighter': 'coderay',
            toc: 'left',
            icons: 'font',
            idprefix: '',
            idseparator: '-',
            sectnums: ''

    dependsOn jrubyPrepare
    gemPath = jrubyPrepare.outputDir
    requires = ['asciidoctor-diagram']
}
asciidoctor.doLast {
    copy {
        // put the index.html in the root of the docs
        from("$asciidoctorOutputDir/html5/$project.name/src/asciidoc") {
            include '**/index.html'
            includeEmptyDirs false
            eachFile { FileCopyDetails details ->
                details.path = details.name
            }
        }
        into file("$dockerBuildDir/docs")
    }
    copy {
        // put the images directory in the root of the docs
        from("$asciidoctorOutputDir/html5/$project.name/src/asciidoc") {
            include 'images/**/*'
            includeEmptyDirs false
            eachFile { FileCopyDetails details ->
                details.path = details.relativeSourcePath
            }
        }
        into file("$dockerBuildDir/docs")
    }
    copy {
        // Add the labs and solutions,
        // and their html versions of the asciidoctor files
        // in the root of the docs
        from("$asciidoctorOutputDir/html5") {
            ['labs'].each {
                include "${it}/**/*.html"
            }
            includeEmptyDirs false
        }
        from(rootDir) {
            ['labs', 'docker-images', 'selenium-grid-docker'].each { dir ->
                include "${dir}/**"
                excludePatterns.each { pattern ->
                    exclude "${dir}/${pattern}"
                }
                exclude "${dir}/**/*.adoc"
            }
            includeEmptyDirs false
        }
        into file("$dockerBuildDir/docs")
    }
}
build.dependsOn asciidoctor

clean {
    delete(fileTree(rootDir) {
        ['labs', 'solutions', projectDir.name].each { dir ->
            excludePatterns.each { pattern ->
                include "${dir}/${pattern}"
            }
        }
    })
}

distributions {
    main {
        def buildDate = new SimpleDateFormat("YYYY-MM-dd-HHmm").format(new Date())
        baseName = "${buildDate}-${rootProject.name}"
        contents {
            from "$dockerBuildDir/docs"
        }
    }
}

task createDockerfile(dependsOn: build) {
    def dockerFile = file("$dockerBuildDir/Dockerfile")
    outputs.files dockerFile

    doLast {
        if (!dockerFile.isFile()) {
            dockerFile << """\
                FROM registry.dev.bol.com/3rdparty/nginx:1.9.3

                ADD docs /usr/share/nginx/html/
                """.stripIndent()
        }
    }
}

task dockerBuild(type: Exec, dependsOn: createDockerfile) {
    description 'Build Docker image of workshop'
    commandLine 'docker', 'build', '--rm', '--tag', "${imageName}:local", dockerBuildDir
}

task cleanRunLocal(type: Exec) {
    description 'Cleanup running workshop Docker container'
    commandLine 'docker', 'rm',
            '--force',
            dockerContainerName
}

task runLocal(type: Exec, dependsOn: dockerBuild) {
    group 'Documentation'
    description 'Run workshop as Docker container'
    doFirst {
        println "See the workshop at: http://localhost"
    }
    commandLine 'docker', 'run',
            '--rm',
            '--publish', '80:80',
            '--tty',
            '--name', dockerContainerName,
            "${imageName}:local"
}
runLocal.finalizedBy cleanRunLocal
